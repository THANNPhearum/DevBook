// This section tells Gradle where to find everything it needs.
buildscript {
    dependencies {
        classpath 'com.jakewharton.hugo:hugo-plugin:1.2.1'
        classpath 'com.github.alexrichards.gradle:ricotta-plugin:1.0.+'
        classpath 'com.goldengekko.gradle:testfairy-plugin:1.0.0.0'
        classpath 'com.goldengekko.gradle:android-release-plugin:1.0.0.2@jar'
    }
    repositories {
        mavenLocal()
        maven { url 'https://nexus.goldengekko.com/nexus/content/groups/public/' }
    }
}

// This section tells Gradle where to find everything your app needs.
repositories {
    //Uncomment if you include Bugsense
    //maven { url 'http://www.bugsense.com/gradle/' }
    //Uncomment if you include facebook
    //maven { url "http://mente.github.io/facebook-api-android-aar" }

}

// Here we're applying the plugins we want to use in the build script. 'android' is the most important.
apply plugin: 'com.android.application'
apply plugin: 'com.jakewharton.hugo'
apply plugin: 'release-helper-plugin'
apply from: '../checkstyle.gradle'
apply from: '../pmd.gradle'
apply from: '../findbugs.gradle'
//apply plugin: 'ricotta'
//apply plugin: 'android-quality'
//apply plugin: 'testfairy'

// Writing a library? Delete what's above and uncomment this.
//apply plugin: 'android-library'
releaseHelper {
    //This can be either Build, Patch, Minor, or Major (defaults to Build if not present)
    releaseType 'Build'
    //This message will be used as tag message (there is a default message if not present)
    //tagDescription 'End of my awesome sprint'
    //This allows you to chose the remote branch you want to point to (defaults to master if not present)
    //remoteBranchName 'my-awesome-branch'
}

// Where to find Ricotta resources.
//ricotta {
//    projectName 'TemplateApplication'
//    host 'ricotta-ost.appspot.com'
//    translation {
//        language 'en'
//        target file('src/main/res/values/ricotta.xml')
//    }
//    translation {
//        language 'es'
//        target file('src/main/res/values-es/ricotta.xml')
//    }
//}


// Android configuration goes here.
android {
    // Choose your platform.
    compileSdkVersion 22
    buildToolsVersion '21.1.2'

    // These options apply to all builds.
    defaultConfig {
        // These are applied to the manifest, to prevent nasty compat modes, use the latest you can.
        minSdkVersion 14
        targetSdkVersion 22

        // Use the project version as the version name, and generate a version code.
        versionName version
        versionCode versionCode(version)

        // Limit the apk's languages to ones that we have translations for, Play Services includes loads we don't need.
        //resConfigs ricotta.resourceConfig

        // You are writing tests, yeah?
        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'

    }

    // Keystore storage and management needs finalising, bt look, we can write code in our build script to manage all that!
    final File keystore = file('blah.keystore');
    final File keystorePassword = file('blah.keystore.password');
    if (keystore.exists() && keystore.canRead()
            && keystorePassword.exists() && keystorePassword.canRead()) {
        final String password = keystorePassword.text.trim()
        signingConfigs {
            release {
                storeFile keystore
                storePassword password
                keyAlias 'android'
                keyPassword password
            }
        }
    }

    // Use build types to customise builds to your needs.
    buildTypes {
        release {
            zipAlignEnabled true
        }

        debug {
            applicationIdSuffix '.debug'
            versionNameSuffix '-SNAPSHOT'
        }

        // Here we're adding a release signing profile, if we found the keystore.
        if (signingConfigs.hasProperty('release')) {
            release.signingConfig signingConfigs.release
            printKeystoreInfos(keystore, keystorePassword)
        }

        //Rename our apk files to give to include the version number
        applicationVariants.all { variant ->
            variant.outputs.each { output ->
                //Apk that will be renamed
                File file = output.outputFile;
                output.outputFile = addVersionToApkName(file)
            }
        }
    }

    // Use product flavours to customise builds
    // Add another flavour group to add more customisation
    flavorDimensions 'server', 'developer'

    productFlavors {
        // Servers
        dev {
            dimension 'server'
        }
        stage {
            dimension 'server'
        }
        prod {
            dimension 'server'
        }

        // Developers
        Jandroid {
            dimension 'developer'
        }
        Alex {
            dimension 'developer'
        }
    }

    // A lot of 3rd party libraries contain similar stuff.
    packagingOptions {
        exclude 'LICENSE.*'
        exclude 'LICENSE.txt'
        exclude 'NOTICE.*'
        exclude 'META-INF/services/javax.annotation.processing.Processor'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/notice.txt'
    }

    // One day, we'll change this to true...
    lintOptions {
        abortOnError false
    }
}

// Use http://gradleplease.appspot.com/ to find dependency names.
dependencies {
    // AppCompat
    compile 'com.android.support:appcompat-v7:22.1.1'
    compile 'com.android.support:cardview-v7:22.0.+'
    compile 'com.android.support:recyclerview-v7:22.0.+'

    // Google Play Services
    compile 'com.google.android.gms:play-services-base:7.5.0'
    compile 'com.google.android.gms:play-services-analytics:7.5.0'

    // Square libraries
    // Network stuff
    compile 'com.squareup.okhttp:okhttp:2.4.0'
    compile 'com.squareup.okhttp:okhttp-urlconnection:2.4.0'
    compile 'com.squareup.retrofit:retrofit:1.9.0'
    compile 'com.squareup.picasso:picasso:2.5.2'
    // Injection stuff
    provided 'com.squareup.dagger:dagger-compiler:1.2.2'
    compile 'com.squareup.dagger:dagger:1.2.2'
    compile 'com.jakewharton:butterknife:6.0.0'

    //ObservableScrollView
    compile 'com.github.ksoichiro:android-observablescrollview:1.5.0'
    compile 'com.nineoldandroids:library:2.4.0'

    //Floating Button
    compile 'com.melnykov:floatingactionbutton:1.0.7'

    //Material Drawer
    compile 'com.mikepenz:materialdrawer:3.1.2@aar'
    //Custom Font
    compile 'com.mikepenz:iconics-core:1.7.1@aar'
    compile 'com.mikepenz:google-material-typeface:1.2.0@aar'

    // Database
    compile 'com.j256.ormlite:ormlite-android:4.48'

    // Log
    compile 'com.jakewharton.timber:timber:2.5.0'

    //Uncomment if you want to include bugsense
    //compile 'com.splunk.mint:mint:3.5'
    //Uncomment if you want to include facebook
    //compile('com.facebook:facebook-android-sdk:+@aar') {
    //transitive = true
    //exclude group: 'com.google.android', module: 'support-v4'
    //}
    // Android Instrumentation testing only dependencies
    androidTestCompile 'com.android.support.test:testing-support-lib:0.1'
    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.0'
    // Android unit testing only dependencies
    // TODO can we minimise these??
    testCompile 'junit:junit:4.12'
    testCompile 'org.hamcrest:hamcrest-core:1.1'
    testCompile 'org.hamcrest:hamcrest-library:1.1'
    testCompile 'org.hamcrest:hamcrest-integration:1.1'
    // Robolectric dependency not fetched in org.robolectric:robolectric:3.0-rc2
    testCompile 'org.apache.maven:maven-ant-tasks:2.1.3'
    testCompile 'org.robolectric:robolectric:3.0-rc2'
    testCompile('com.squareup.assertj:assertj-android:1.0.0') {
        exclude group: 'com.android.support', module: 'support-annotations'
    }

}

configurations {
    // to avoid double inclusion of support libraries
//    all*.exclude group: 'com.android.support', module: 'support-v4'
    all*.exclude module: 'classworlds'
    all*.exclude module: 'commons-logging'
    all*.exclude module: 'httpclient'
    all*.exclude module: 'maven-artifact'
    all*.exclude module: 'maven-artifact-manager'
    all*.exclude module: 'maven-error-diagnostics'
    all*.exclude module: 'maven-model'
    all*.exclude module: 'maven-project'
    all*.exclude module: 'maven-settings'
    all*.exclude module: 'plexus-container-default'
    all*.exclude module: 'plexus-interpolation'
    all*.exclude module: 'plexus-utils'
    all*.exclude module: 'wagon-file'
    all*.exclude module: 'wagon-http-lightweight'
    all*.exclude module: 'wagon-provider-api'
    all*.exclude module: 'javax.annotation-api'
}


def int versionCode(String versionName) {
    int versionCode = 0
    versionName.split('\\.').collect { part ->
        Integer.parseInt(part, 10)
    }.eachWithIndex { part, i ->
        versionCode += part * Math.pow(10, (4 - i - 1) * 3)
    }
    return versionCode
}

/**
 * This method takes a file in and will replace
 * the default app name with a name we chose and
 * will append the version number at the end to
 * make versions easy to track
 *
 * @param file
 * @return new file name
 */
def File addVersionToApkName(File file) {
    String apkName = file.name.replace(".apk", "-" + version + ".apk")
    return new File(file.parent, apkName)
}

/**
 * This method is used for debugging purposes, it allows you
 * to get a quick look at the keystore status on Jenkins
 *
 * @param keystore
 * @param keystorePassword
 * @return
 */
def printKeystoreInfos(File keystore, File keystorePassword) {
    println("keystore exists => " + keystore.exists())
    println("keystore can read => " + keystore.canRead())
    println("keystorePassword exists => " + keystorePassword.exists())
    println("keystorePassword can read => " + keystorePassword.canRead())
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.3'
}

//TestFairy start - autogenerated by TestFairy intellij plugin
//manual changes might get overwritten
buildscript {
    repositories {
        mavenCentral()
        maven { url 'https://www.testfairy.com/maven' }
    }
    dependencies {
        classpath 'com.testfairy.plugins.gradle:testfairy:1.+'
    }
}
apply plugin: 'testfairy'
android {
    testfairyConfig {
        apiKey '76ef05a3abf0f5cf663828fe98667e6475350527'
    }
}
//TestFairy end
